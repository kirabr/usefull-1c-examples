Функция РазделитьСтрокуПробелами(СтрокаБезПробелов) Экспорт

	СтрокаСПробелами = СтрокаБезПробелов;
	
	// Подчёркивания, двойные пробелы - сокращаем
	СтрокаСПробелами = СокрЛП(СтрЗаменить(СтрЗаменить(СтрокаСПробелами, "_", " "), "  ", " "));
	
	// Обратным проходом вставляем пробелы перед заглавными буквами
	ТекПоз = СтрДлина(СтрокаСПробелами);
	Пока ТекПоз > 1 Цикл
		Если Сред(СтрокаСПробелами, ТекПоз, 1) = ВРег(Сред(СтрокаСПробелами, ТекПоз, 1)) Тогда
			СтрокаСПробелами = Лев(СтрокаСПробелами, ТекПоз - 1) + " " + Сред(СтрокаСПробелами, ТекПоз);	
		КонецЕсли; 
		ТекПоз = ТекПоз - 1; 
	КонецЦикла;
	
	// Двойные пробелы - сокращаем
	СтрокаСПробелами = СтрЗаменить(СтрокаСПробелами, "  ", " ");
	
	// Прямым проходом склеиваем заглавные, разделённые пробелами - это, вероятно, аббревиатуры ("Э Д О" -> "ЭДО")
	ТекПоз = 1;
	Пока Истина Цикл
		Если ВРег(Сред(СтрокаСПробелами, ТекПоз, 3)) = Сред(СтрокаСПробелами, ТекПоз, 3) 
			И Не ПустаяСтрока(Сред(СтрокаСПробелами, ТекПоз, 1)) 
			И ПустаяСтрока(Сред(СтрокаСПробелами, ТекПоз + 3, 1)) Тогда 		
			СтрокаСПробелами = Лев(СтрокаСПробелами, ТекПоз) + Сред(СтрокаСПробелами, ТекПоз + 2);
		КонецЕсли;
		ТекПоз = ТекПоз + 1;
		Если ТекПоз > СтрДлина(СтрокаСПробелами) - 1 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Двойные пробелы - сокращаем
	СтрокаСПробелами = СтрЗаменить(СтрокаСПробелами, "  ", " ");
	
	// Выполняем аналог Трег(), только учитывая аббревиатуры внутри строки ("Формирование ЭДО Документов" -> "Формирование ЭДО документов")
	Подстроки = СтрРазделить(СтрокаСПробелами, " ");
	СтрокаСПробелами = "";
	Для Инд = 0 По Подстроки.ВГраница() Цикл
		СтрокаСПробелами = СтрокаСПробелами + ?(ПустаяСтрока(СтрокаСПробелами), "", " ") 
		+ ?(СтрДлина(Подстроки[Инд]) > 1 И Подстроки[Инд] = ВРег(Подстроки[Инд]), Подстроки[Инд], ?(Инд = 0, Подстроки[Инд], НРег(Подстроки[Инд])));
	КонецЦикла;
	 
	Возврат СтрокаСПробелами; 

КонецФункции // РазделитьСтрокуПробелами()